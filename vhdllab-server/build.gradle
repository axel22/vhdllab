apply plugin: 'war'
apply plugin: 'jetty'

war.archiveName = 'vhdllab.war'

def fromRootProjectProperty = { name ->
    { line -> line.replace('${' + name + '}', rootProject.ext[name]) }
}

processResources.doFirst {
    if (project.ext.has('ghdl.tmp.dir')) {
        filter fromRootProjectProperty('ghdl.tmp.dir')
    }
    filter fromRootProjectProperty('ghdl.compilation.cmd')
    filter fromRootProjectProperty('ghdl.simulation.cmd')
    filter fromRootProjectProperty('app.context')
}

war {
    filter fromRootProjectProperty('app.host')
    filter fromRootProjectProperty('app.context')
    filter fromRootProjectProperty('http.port')
    filter fromRootProjectProperty('database.connection.url')
    filter fromRootProjectProperty('database.connection.username')
    filter fromRootProjectProperty('database.connection.password')
    filter { line -> line.replace('${application.version}', rootProject.version) }
}

configurations.all {
    resolutionStrategy {
        force 'xml-apis:xml-apis:1.4.01'
    }
}

dependencies {
    compile project(':vhdllab-common')
    compile 'org.hibernate:hibernate-validator:3.1.0.GA'
    compile 'org.hibernate:hibernate-entitymanager:3.4.0.GA'
    compile 'org.springframework:spring-webmvc:2.5.6'
    compile 'org.springframework:spring-aspects:2.5.6', { exclude(module: 'aspectjrt') }
    compile 'org.springframework:spring-orm:2.5.6'
    compile 'org.springframework:spring-jdbc:2.5.6'

    compile 'org.springframework:spring-aop:2.5.6' // override spring-security-core
    compile 'org.springframework.security:spring-security-core:2.0.5.RELEASE', { exclude(module: 'spring-support') }
    compile 'jaxen:jaxen:1.1.1'

    // http://stackoverflow.com/questions/10482563/aop-java-lang-illegalargumentexception-error-at-0-cant-find-referenced-poi
    compile 'org.aspectj:aspectjweaver:1.7.3'

    compile 'org.apache.commons:commons-exec:1.1'
    providedCompile 'javax.servlet:servlet-api:2.5'
    runtime 'org.hibernate:hibernate-ehcache:3.3.2.GA'
    runtime 'commons-dbcp:commons-dbcp:1.4'
    runtime 'mysql:mysql-connector-java:5.1.13'
    runtime 'hsqldb:hsqldb:1.8.0.7' // easier development

    // JNLP Download Servlet
    runtime 'org.codehaus.mojo.webstart:webstart-jnlp-servlet:1.0-6.0-02_ea_b02'

    runtime 'org.slf4j:slf4j-log4j12:1.6.1'
    testCompile 'junit:junit:4.4'
    testCompile 'org.springframework:spring-test:2.5.6'
    testCompile 'hsqldb:hsqldb:1.8.0.7'
}
